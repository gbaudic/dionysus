import org.cyclonedx.model.*

plugins {
  id "java"
  id "eclipse"
  id "application"
  id "org.sonarqube" version "7.2.2.6593"
  id "org.cyclonedx.bom" version "3.2.0"
  id "com.diffplug.spotless" version "8.2.1"
}

mainClassName = 'net.sourceforge.dionysus.gui.MainGUI2'

distributions {
	main {
		contents {
			from { 'COPYING' }
			from { 'CHANGELOG' }
			from { 'log.dat' }
			from { 'accounts.dat' }
			from { 'articles.dat' }
			from { 'logins.txt' }
		}
	}
}

repositories {
	mavenLocal()
    mavenCentral() 
}

dependencies {
    testImplementation 'junit:junit:4.13.2' 
}

sonar {
  properties {
    property "sonar.projectKey", "gbaudic_dionysus"
    property "sonar.organization", "gbaudic-github"
    property "sonar.host.url", "https://sonarcloud.io"
  }
}

tasks.cyclonedxDirectBom {
    projectType = Component.Type.APPLICATION
    includeConfigs = ["runtimeClasspath"]
    licenseChoice = new LicenseChoice(licenses: [new License(name: "GPL-3.0-or-later", url: "https://spdx.org/licenses/GPL-3.0-or-later.html")])
    componentVersion = '0.3.0'
    xmlOutput.unsetConvention()
}

spotless {
    java {
        importOrder()
        removeUnusedImports()
        eclipse()
        endWithNewline()
        licenseHeader("/* 	Dionysus, a student pub management software in Java\n" +
            "    Copyright (C) 2011,2013,2015-2019  G. Baudic\n" +
            "\n" +
            "    This program is free software: you can redistribute it and/or modify\n" +
            "    it under the terms of the GNU General Public License as published by\n" +
            "    the Free Software Foundation, either version 3 of the License, or\n" +
            "    (at your option) any later version.\n" +
            "\n" +
            "    This program is distributed in the hope that it will be useful,\n" +
            "    but WITHOUT ANY WARRANTY; without even the implied warranty of\n" +
            "    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the\n" +
            "    GNU General Public License for more details.\n" +
            "\n" +
            "    You should have received a copy of the GNU General Public License\n" +
            "    along with this program.  If not, see <http://www.gnu.org/licenses/>.\n" +
            "*/")
        
    }
}

jar {
    archiveBaseName = 'dionysus'
    archiveVersion = '0.3.0'
    manifest {
        attributes 'Main-Class': 'net.sourceforge.dionysus.gui.MainGUI2'
    }
}
